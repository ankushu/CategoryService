buildscript {
	ext {
		springBootVersion = '2.0.2.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.vodQA'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('au.com.dius:pact-jvm-consumer-junit_2.12:3.5.14')
	testCompile('au.com.dius:pact-jvm-provider-junit_2.12:3.5.10')
	testCompile('au.com.dius:pact-jvm-provider-spring_2.12:3.5.10')
}

sourceSets {
	consumerContract {
		java.srcDir 'src/test/contract/consumer/java'
		compileClasspath += main.output + test.output
		runtimeClasspath += main.output + test.output
	}

	providerContract {
		java.srcDir 'src/test/contract/provider/java'
		compileClasspath += main.output + test.output
		runtimeClasspath += main.output + test.output
	}
}

configurations {
	consumerContractCompile.extendsFrom testCompile
	consumerContractCompile.extendsFrom testRuntime

	providerContractCompile.extendsFrom testCompile
	providerContractCompile.extendsFrom testRuntime
}

task consumerContractTests(type: Test, description: 'Runs the consumer contract tests.', group: 'Verification') {
	testClassesDir = sourceSets.consumerContract.output.classesDir
	classpath = sourceSets.consumerContract.runtimeClasspath
}

task providerContractTests(type: Test, description: 'Runs the provider contract tests.', group: 'Verification') {
	testClassesDir = sourceSets.providerContract.output.classesDir
	classpath = sourceSets.providerContract.runtimeClasspath
}